local kap = import 'lib/kapitan.libjsonnet';
local kube = import 'lib/kube.libjsonnet';
local inv = kap.inventory();
local params = inv.parameters.system_upgrade_controller;
{
  Plan(name, channel, version, label_selectors, concurrency, tolerations, image, command, args):

    kube._Object('upgrade.cattle.io/v1', 'Plan', name) {
      metadata+: {
        namespace: params.namespace,
        annotations+: {
          'argocd.argoproj.io/sync-options': 'SkipDryRunOnMissingResource=true',
        },
        labels+: {
          'app.kubernetes.io/managed-by': 'syn',
        },
      },
      spec: {
        concurrency: concurrency,
        nodeSelector: {
          matchExpressions: label_selectors,
        },
        serviceAccountName: params.service_account,
        tolerations: tolerations,
        drain: {
          force: true,
        },
        [if channel != null then 'channel']: channel,
        [if version != null then 'version']: version,
        upgrade: {
          image: image,
          [if command != null then 'command']: command,
          [if args != null then 'args']: args,
        },
      },
    },
}
